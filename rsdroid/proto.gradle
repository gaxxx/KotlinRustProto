import org.gradle.internal.os.OperatingSystem

def droidProtobufFolder = new File(rootDir, "rslib-bridge/proto").getAbsolutePath()

android {
    sourceSets {
        main {
            proto {
                srcDir droidProtobufFolder
            }
        }
    }
}

protobuf {
    String protocGenPath = OperatingSystem.current().isWindows() ? 'tools\\protoc-gen\\protoc-gen.bat' : 'tools/protoc-gen/protoc-gen.sh'
    File f = new File(project.rootDir, protocGenPath)
    if (!f.exists()) {
        throw new IllegalStateException("'${f.absolutePath}' does not exist")
    }
    System.out.println("protoc-gen: '${f.absolutePath}'")
    plugins {
        javalite {
            artifact = 'com.google.protobuf:protoc-gen-javalite:3.0.0'
        }
    }
    plugins {
        // Define a plugin with name 'anki'.
        native_rpc { path = f.absolutePath }
    }
    protoc {
        artifact = 'com.google.protobuf:protoc:3.8.0'
    }
    // this is a task which wil generate classes for our proto files
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                remove java
            }
            task.plugins {
                javalite {}
            }
            task.plugins {
                native_rpc {}
            }
        }
    }
}